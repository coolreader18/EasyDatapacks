snap = #e

def send_home block:

	at block
		setblock ~ ~ ~ air
	as @e[name=BlockSpawner]
		scoreboard players operation @s track_test = @s track_id
		scoreboard players operation @s track_test -= block#1 track_id
	tp block @e[name=BlockSpawner,scores={track_test=0},limit=1]
	at block
		setblock ~ ~ ~ dark_prismarine

def delete block:

	at block
		setblock ~ ~ ~ air
	as @e[name=BlockSpawner]
		scoreboard players operation @s track_test = @s track_id
		scoreboard players operation @s track_test -= block#1 track_id
	kill @e[name=BlockSpawner,scores={track_test=0}]
	kill block

def load:

	scoreboard objectives add track_id dummy
	scoreboard objectives add track_test dummy
	scoreboard objectives add push_cooldown dummy
	tellraw @a {"text":"The MovingBlocks datapack has been loaded successfully!","color":"green"}

def tick:

	# summoning new renewable blocks
	as @e[type=item_frame,nbt={Item:{id:"minecraft:dark_prismarine"}}] at @s
		summon armor_stand ~ ~ ~ {CustomName:"\"BlockTracker\"",NoGravity:1,Invisible:1,Marker:1}
		summon armor_stand ~ ~ ~ {CustomName:"\"BlockSpawner\"",NoGravity:1,Invisible:1,Marker:1}
		scoreboard players add @e[name=BlockTracker] track_id 1
		scoreboard players add @e[name=BlockSpawner] track_id 1

		particle firework ~ ~1 ~ 0 0 0 0.04 100
		setblock ~ ~ ~ dark_prismarine
		kill @s

	# debug tool: highlight spawners/trackers when holding a dark prismarine
	if entity @a[nbt={SelectedItem:{id:"minecraft:dark_prismarine"}}]
		at @e[name=BlockTracker]
			particle flame ~ ~1.5 ~ 0 0 0 0 10
		at @e[name=BlockSpawner]
			particle firework ~ ~1 ~ 0 0 0 0 10

	# delete trackers by placing a black wool above them
	as @e[name=BlockTracker] at @s if block ~ ~1 ~ black_wool
		setblock ~ ~1 ~ air
		delete @s

	# handle armor stand tracking
	as @e[name=BlockTracker] at @s
		if entity @e[name=MovingBlock,distance=..1] if block ~ ~ ~ air at @e[name=MovingBlock,distance=..1,limit=1,sort=nearest] rotated as @s
			tp @s ~ ~ ~
		if block ~ ~-1 ~ dark_prismarine
			tp @s ~ ~-1 ~
		if block ~ ~ ~ dark_prismarine if block ~ ~-1 ~ black_wool
			send_home @s

	# control players pushcooldown
	scoreboard players add @a push_cooldown 1
	as @a at @s unless block ~0.31 ~ ~ dark_prismarine unless block ~-0.31 ~ ~ dark_prismarine unless block ~ ~ ~0.31 dark_prismarine unless block ~ ~ ~-0.31 dark_prismarine
		scoreboard players set @s push_cooldown 0

	# snap blocks back to location once moved 1 block
	as @e[type=falling_block,name=MovingBlock,nbt={Time:4}] at @s
		if block ~ ~-1 ~ air
			tp @s ~ ~-0.02 ~
			data merge entity @s {NoGravity:0}
		else
			if block ~ ~-1 ~ packed_ice
				tag @s add Slippery
			else
				setblock ~ ~ ~ dark_prismarine
				kill @s

	# handle blocks on ice
	as @e[type=falling_block,name=MovingBlock,tag=Slippery,nbt={NoGravity:1b}] at @s
		# over air, switch to fall mode
		if block ~ ~-1 ~ air
			tp @s ~ ~-0.02 ~
			data merge entity @s {NoGravity:0}
		else
			rotated as @e[limit=1,name=BlockTracker,sort=nearest] unless block ^ ^ ^0.55 air
				setblock ~ ~ ~ dark_prismarine
				kill @s
			rotated as @e[limit=1,name=BlockTracker,sort=nearest] unless block ~ ~-1 ~ packed_ice
				setblock ~ ~ ~ dark_prismarine
				kill @s

	# block launch mechanics
	at @a[scores={push_cooldown=12..}]

		# motion - east
		if block ~0.31 ~ ~ dark_prismarine if block ~1.31 ~ ~ air
			at @p
				setblock ~0.31 ~ ~ air
				snap = summon item_frame ~0.31 ~ ~ {Facing:4}
			at snap
				summon falling_block ~-0.48 ~-0.5 ~ {BlockState:{Name:"dark_prismarine"},Time:1,NoGravity:1,Motion:[0.25,0.0,0.0],CustomName:"\"MovingBlock\""}
			kill snap
			at @p as @e[type=armor_stand,name=BlockTracker,limit=1,sort=nearest] at @s
				tp @s ~ ~ ~ 270 0

		# motion - west
		if block ~-0.31 ~ ~ dark_prismarine if block ~-1.31 ~ ~ air
			at @p
				setblock ~-0.31 ~ ~ air
				snap = summon item_frame ~-0.31 ~ ~ {Facing:5}
			at snap
				summon falling_block ~0.48 ~-0.5 ~ {BlockState:{Name:"dark_prismarine"},Time:1,NoGravity:1,Motion:[-0.25,0.0,0.0],CustomName:"\"MovingBlock\""}
			kill snap
			at @p as @e[type=armor_stand,name=BlockTracker,limit=1,sort=nearest] at @s
				tp @s ~ ~ ~ 90 0

		# motion - north
		if block ~ ~ ~0.31 dark_prismarine if block ~ ~ ~1.31 air
			at @p
				setblock ~ ~ ~0.31 air
				snap = summon item_frame ~ ~ ~0.31 {Facing:2}
			at snap
				summon falling_block ~ ~-0.5 ~-0.48 {BlockState:{Name:"dark_prismarine"},Time:1,NoGravity:1,Motion:[0.0,0.0,0.25],CustomName:"\"MovingBlock\""}
			kill snap
			at @p as @e[type=armor_stand,name=BlockTracker,limit=1,sort=nearest] at @s
				tp @s ~ ~ ~ 0 0

		# motion - south
		if block ~ ~ ~-0.31 dark_prismarine if block ~ ~ ~-1.31 air
			at @p
				setblock ~ ~ ~-0.31 air
				snap = summon item_frame ~ ~ ~-0.31 {Facing:3}
			at snap
				summon falling_block ~ ~-0.5 ~0.48 {BlockState:{Name:"dark_prismarine"},Time:1,NoGravity:1,Motion:[0.0,0.0,-0.25],CustomName:"\"MovingBlock\""}
			kill snap
			at @p as @e[type=armor_stand,name=BlockTracker,limit=1,sort=nearest] at @s
				tp @s ~ ~ ~ 180 0