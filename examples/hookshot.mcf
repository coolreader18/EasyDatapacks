clickdummy = @
hookhead = @
mode = @
length = @

def interact:

	at hookhead
		if block ~ ~1 ~ minecraft:gold_block
			# hit a gold block
			at hookhead
				kill @e[name=HookshotChain,distance=..0.5]
				tp hookhead ^ ^ ^-1

			effect clear @p minecraft:slowness
			effect clear @p minecraft:jump_boost
			effect give @p minecraft:levitation 255 255 true

			# switch to pull mode
			mode = 3

		else
			# hit something else, switch to retract mode
			playsound minecraft:block.anvil.land master @a ~ ~ ~ 1 1.4 0.5
			mode = 2

def reset:

	mode = 0
	effect clear @p minecraft:slowness
	effect clear @p minecraft:jump_boost
	effect clear @p minecraft:levitation
	kill hookhead
	give @p[nbt=!{SelectedItem:{id:"minecraft:iron_horse_armor"}}] minecraft:iron_horse_armor{display:{Name:"{\"text\":\"Hookshot\",\"italic\":false}",Lore:["Right click to use:","- latches onto gold blocks","- retrieves items for you"]}}

def load:

	kill @e[name=HookshotHead]
	kill @e[name=ClickDummy]
	mode = 0
	tellraw @a {"text":"The Hookshot datapack has been loaded successfully!","color":"green"}

def tick:
	
	# if the clickdummy is missing, replace it
	unless entity clickdummy
		clickdummy = summon minecraft:armor_stand 0 0 0 {Invulnerable:1,ShowArms:1,NoGravity:1,Invisible:1,CustomName:"\"ClickDummy\""}

	# clickdummy follows the player around
	if entity @p[nbt={SelectedItem:{id:"minecraft:iron_horse_armor"}}]
		tp clickdummy @p
	else
		tp clickdummy 0 0 0

	# detect clickdummy receiving an item into its hand
	as clickdummy[nbt={HandItems:[{id:"minecraft:iron_horse_armor"},{}]}]
		data merge entity clickdummy#1 {HandItems:[{},{}]}

		# create the hookshot head
		unless entity hookhead at @p
			hookhead = summon minecraft:armor_stand ~ ~ ~ {CustomName:"\"HookshotHead\"",NoGravity:1,Invulnerable:1,ArmorItems:[{},{},{},{id:"minecraft:quartz_block",Count:1b,Damage:0s}],Small:1,Invisible:1}

		# move the hookhead to its starting position
		as @p
			tp hookhead @s
		at @p
			tp hookhead ~ ~0.4 ~
		length = 0
		mode = 1

		effect give @p minecraft:slowness 255 255 true
		effect give @p minecraft:jump_boost 255 137 true

	# extension
	if mode == 1

		repeat 2
			at hookhead
				tp hookhead ^ ^ ^0.5

				# create the hook chain
				if block ~ ~1 ~ minecraft:air
					summon minecraft:armor_stand ~ ~ ~ {CustomName:"\"HookshotChain\"",NoGravity:1,Invulnerable:1,ArmorItems:[{},{},{},{id:"minecraft:stone_button",Count:1b,Damage:0s}],Small:1,Invisible:1}
				length += 1
				playsound minecraft:block.dispenser.fail master @p ~ ~ ~ 1 1.4

				# latch onto any nearby items
				positioned ~ ~1 ~ if entity @e[distance=..0.5,type=item]
					tag @e[distance=..0.5,type=item] add Hooked

				# stop extending if we hit a wall or reach max length
				if block ~ ~1 ~ air 
					if length >= 40
						interact
				else
					interact

	# retraction
	if mode == 2

		repeat 2
			at hookhead
				tp hookhead ^ ^ ^-0.5

				length -= 1
				kill @e[name=HookshotChain,distance=..0.7]
				playsound minecraft:block.dispenser.fail master @a ~ ~ ~ 1 1.4

				# pull along any hooked items
				tp @e[tag=Hooked] hookhead#1

				# hookhead is completely retracted
				if length <= 0
					reset

	# pulling
	if mode == 3 at hookhead

		repeat 2
			as @p at @p
				# move the player
				at @e[name=HookshotChain,limit=1,sort=nearest] facing entity hookhead#1 eyes
					tp @p ~ ~ ~

				kill @e[name=HookshotChain,distance=..0.1]

			at hookhead
				playsound minecraft:block.dispenser.fail master @a ~ ~ ~ 1 1.4

			# chain is completely gone, reset
			unless entity @e[name=HookshotChain]
				reset