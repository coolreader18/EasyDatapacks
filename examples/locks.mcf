def load:

	tellraw @a {"text":"The Locks datapack has been loaded successfully!","color":"green"}

# command to move the display/interface armor stands according to an item frame.
# a good example of a command which takes many parameters.
def orient display interface frame:

	if entity frame[nbt={Facing:2b}]
		tp display ~ ~-2.7 ~0.7 0 0
		tp interface ~ ~-1.5 ~0.53
	if entity frame[nbt={Facing:3b}]
		tp display ~ ~-2.7 ~-0.7 180 0
		tp interface ~ ~-1.5 ~-0.53
	if entity frame[nbt={Facing:5b}]
		tp display ~-0.7 ~-2.7 ~ 90 0
		tp interface ~-0.53 ~-1.5 ~
	if entity frame[nbt={Facing:4b}]
		tp display ~0.7 ~-2.7 ~ 270 0
		tp interface ~0.53 ~-1.5 ~

# given an interface armor stand, open the corresponding door
def open interface:

	at interface:
		playsound block.iron_door.open master @p ~ ~ ~ 1 1 1
		fill ~-1 ~-1 ~-1 ~1 ~3 ~1 air replace iron_bars
		particle block iron_bars ~ ~1 ~ 0.25 0.5 0.25 0 100

		if entity interface[name=LockInterfaceIron]
			kill @e[type=armor_stand,name=LockDisplayIron,limit=1,sort=nearest]
		else
			kill @e[type=armor_stand,name=LockDisplayGold,limit=1,sort=nearest]

	kill interface

def tick:

	# creating a new iron door
	as @e[type=item_frame,nbt={Item:{id:"minecraft:iron_ingot"}}] at @s

		# create the armor stand display & interface
		particle firework ~ ~ ~ 0 0 0 0.04 100
		d = summon armor_stand ~ ~ ~ {CustomName:"\"LockDisplayIron\"",NoGravity:1,Invisible:1,ArmorItems:[{},{},{},{id:"minecraft:iron_ingot",Count:1}]}
		i = summon armor_stand ~ ~ ~ {CustomName:"\"LockInterfaceIron\"",NoGravity:1,Invisible:1}

		# orient the stands appropriately
		orient d i @s
		kill @s

	# creating a new gold door
	as @e[type=item_frame,nbt={Item:{id:"minecraft:gold_ingot"}}] at @s

		# create the armor stand display & interface
		particle firework ~ ~ ~ 0 0 0 0.04 100
		d = summon armor_stand ~ ~ ~ {CustomName:"\"LockDisplayGold\"",NoGravity:1,Invisible:1,ArmorItems:[{},{},{},{id:"minecraft:gold_ingot",Count:1}]}
		i = summon armor_stand ~ ~ ~ {CustomName:"\"LockInterfaceGold\"",NoGravity:1,Invisible:1}

		# orient the stands appropriately
		orient d i @s
		kill @s

	# if a player is holding a key, show the arms of the nearest corresponding armor stand
	as @a

		if entity @s[nbt={SelectedItem:{id:"minecraft:iron_nugget"}}]
			data merge entity @e[type=armor_stand,name=LockInterfaceIron,limit=1,sort=nearest] {ShowArms:1}
		else
			data merge entity @e[type=armor_stand,name=LockInterfaceIron,limit=1,sort=nearest] {ShowArms:0}

		if entity @s[nbt={SelectedItem:{id:"minecraft:gold_nugget"}}]
			data merge entity @e[type=armor_stand,name=LockInterfaceGold,limit=1,sort=nearest] {ShowArms:1}
		else
			data merge entity @e[type=armor_stand,name=LockInterfaceGold,limit=1,sort=nearest] {ShowArms:0}

	# opening an iron door
	as @e[name=LockInterfaceIron,type=armor_stand,nbt={HandItems:[{id:"minecraft:iron_nugget"},{}]}]
		open @s

	# opening a gold door
	as @e[name=LockInterfaceGold,type=armor_stand,nbt={HandItems:[{id:"minecraft:gold_nugget"},{}]}]
		open @s
